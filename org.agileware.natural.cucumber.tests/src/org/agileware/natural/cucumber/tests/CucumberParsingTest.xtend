/*
 * generated by Xtext
 */
package org.agileware.natural.cucumber.tests

import com.google.inject.Inject
import org.eclipse.xtext.testing.InjectWith
import org.eclipse.xtext.testing.XtextRunner
import org.junit.Test
import org.junit.runner.RunWith

import static org.agileware.natural.cucumber.tests.CucumberTestHelpers.*
import static org.hamcrest.MatcherAssert.*
import static org.hamcrest.Matchers.*

@RunWith(XtextRunner)
@InjectWith(CucumberInjectorProvider)
class CucumberParsingTest {

	@Inject CucumberTestHelpers _th
	
	@Test
	def void helloCucumber() {
		val feature = _th.parse('''
			Feature: Hello, Cucumber!
			  The quick brown fox
			  Jumps over the lazy dog
			
			Scenario: Jack and Jill
			  Given Jack and Jill went up a hill
		''')

		// Should parse without issues
		assertThat(feature, notNullValue())
		assertThat(feature.eResource.errors, empty())
		
		assertThat(feature.title, equalTo("Hello, Cucumber!"))

		val scenarios = feature.scenarios
		assertThat(scenarios, hasSize(1))
		assertThat(scenarios, hasItem(withScenario("Jack and Jill")))
		
		val steps = scenarios.get(0).steps
		assertThat(steps, hasSize(1))
		assertThat(steps, hasItems(
			withStep("Given", "Jack and Jill went up a hill")
		))
	}

//	@Test
//	def void featureOnly() {
//		val feature = _th.parse('''
//			Feature: Hello, Cucumber!
//			  The quick brown fox
//			  Jumps over the lazy dog
//		''')
//
//		assertThat(feature, notNullValue())
//		assertThat(feature.title, equalTo("Hello, Cucumber!"))
//		assertThat(feature.narrative, notNullValue())
//		assertThat(feature.narrative.lines, hasSize(2))
//	}
	
	@Test
	def void scenarioWithBackground() {
		val feature = _th.parse('''
			Feature: Jack and Jill
			
			Background: 
			  Given Jack and Jill went up a hill
			  
			Scenario: Jack falls down
			  When "Jack" falls down
			  Then "Jill" comes tumbling after
		''')

		// Should parse without issues
		assertThat(feature, notNullValue())
		assertThat(feature.eResource.errors, empty())
		
		assertThat(feature.title, equalTo("Jack and Jill"))
		
		val background = feature.background
		assertThat(background, notNullValue())
		assertThat(background.steps, hasItems(
			withStep("Given", "Jack and Jill went up a hill")
		))

		val scenarios = feature.scenarios
		assertThat(scenarios, hasSize(1))
		assertThat(scenarios.get(0).steps, hasItems(
			withStep("When", "\"Jack\" falls down"),
			withStep("Then", "\"Jill\" comes tumbling after")
		))
	}
	
	@Test
	def void stepsWithData() {
		val feature = _th.parse('''
			Feature: Jack and Jill
			
			Scenario: Jack
			  Given the pet
			    | name | status      |
			    | Fido | unavailable |
			  Then the owner
			  """
			  Is sad to see him go
			  """
		''')
		
		// Should parse without issues
		assertThat(feature, notNullValue())
		assertThat(feature.eResource.errors, empty())
	}

	@Test
	def void allSupportedSyntax() {
		// TODO: this is still missing a lot of odd-ball cases
		val feature = _th.parse('''
			@release:Release-2 
			@version:1.0.0
			@pet_store
			Feature: Add a new pet 
				In order to sell a pet
				As a store owner
				I want to add a new pet to the catalog
			
			Background: Add a dog 
				Given I have the following pet
					| name | status    |
					| Fido | available |
				And I add the pet to the store
			
			@add
			@fido
			Scenario: Add a dog 
				Then the pet should be available in the store 
				
			@update
			@fido
			Scenario: Update a dog 
				Given the pet is available in the store
				When I update the pet with  
					| name | status      |
					| Fido | unavailable |
				Then the pet should be unavailable in the store 
			
			@eat-pickles	
			Scenario Outline: Eating pickles
				Given there are <start> pickles
				When I eat <eat> pickles
				Then I should have <left> pickles
			
				@hungry
				Examples:
					| start | eat | left |
					|    12 |  10 |    2 |
					|    20 |  15 |    5 |
			
				@full
				Examples:
					| start | eat | left |
					|    12 |   2 |   10 |
					|    20 |   5 |   15 |
			
		''')

		assertThat(feature, notNullValue())
		assertThat(feature.eResource.errors, empty())
	}
}
