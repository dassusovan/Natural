grammar org.agileware.natural.cucumber.Cucumber hidden(WS, SL_COMMENT)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate cucumber "http://www.agileware.org/natural/cucumber"

Feature:
	tags+=Tag*
	'Feature:'
	title=Title EOL+
	narrative=Narrative?
	background=Background?
	scenarios+=(AbstractScenario)*
;

Background: {Background} 
	'Background:'
	title=Title? EOL+
	narrative=Narrative?
	steps+=Step*
;
	
AbstractScenario: (Scenario | ScenarioOutline);

Scenario: {Scenario} 
	tags+=Tag*
	'Scenario:'
	title=Title? EOL+
	narrative=Narrative?
	steps+=Step*
;

ScenarioOutline: {ScenarioOutline}
	tags+=Tag*
	'Scenario' 'Outline:'
	title=Title? EOL+
	narrative=Narrative?
	steps+=Step*
	examples+=Example+
;
	
Step:
	keyword=STEP_KEYWORD
	description=StepDescription EOL*
	(table=Table | text=DocString)?
;

Example:
	tags+=Tag*
	'Examples:' 
	title=Title? EOL+
	narrative=Narrative?
	table=Table
;
	
Table:
	rows+=TableRow+
	EOL?
;

TableRow:
	cols+=TableCol+
	TABLE_ROW_END
;

TableCol: '|'
	cell=TableCell
;

TableCell: (WORD | INT | STRING | PLACEHOLDER | STEP_KEYWORD | TAGNAME)+;
	
DocString:
	value=DOC_STRING
	EOL*
;

Title:
	(WORD | INT | STRING | PLACEHOLDER)
	(WORD | INT | STRING | PLACEHOLDER | STEP_KEYWORD | TAGNAME)*
;

Narrative:
    lines+=NarrativeLine+
    ( -> EOL?)
;

NarrativeLine:
    value=LineValue
    EOL
;

LineValue:
	(WORD | INT | STRING | PLACEHOLDER)
	(WORD | INT | STRING | PLACEHOLDER | STEP_KEYWORD | TAGNAME)*
;

StepDescription:
	(WORD | INT | STRING | PLACEHOLDER | STEP_KEYWORD | TAGNAME)+;
	
Tag: id=TAGNAME EOL?;

terminal INT: '-'? ('0'..'9')+ ('.' ('0'..'9')+)?;

terminal STEP_KEYWORD: ('Given' | 'When' | 'Then' | 'And' | 'But');

terminal PLACEHOLDER: '<' !('>' | ' ' | '\t' | '\n' | '\r')+ '>';
	
terminal TABLE_ROW_END: '|' (NL | EOF);

terminal DOC_STRING: ('"""' -> '"""' | "'''" -> "'''") NL;

terminal STRING:
	'"' ('\\' ('b' | 't' | 'n' | 'f' | 'r' | 'u' | '"' | "'" | '\\') | !('\\' | '"' | '\r' | '\n'))* '"' |
	"'" ('\\' ('b' | 't' | 'n' | 'f' | 'r' | 'u' | '"' | "'" | '\\') | !('\\' | "'" | '\r' | '\n'))* "'";

terminal SL_COMMENT: '#' !('\n' | '\r')* NL;

terminal TAGNAME: '@' !(' ' | '\t' | '\n' | '\r')+ ;

terminal WORD: !('@' | '|' | ' ' | '\t' | '\n' | '\r') !(' ' | '\t' | '\n' | '\r')*;

terminal WS: (' ' | '\t');

terminal EOL: NL;

terminal fragment NL: ('\r'? '\n');
