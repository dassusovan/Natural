/*
 * generated by Xtext
 */
package org.agileware.natural.jbehave.tests

import com.google.inject.Inject
import org.agileware.natural.jbehave.jbehave.NarrativeA
import org.agileware.natural.jbehave.jbehave.NarrativeB
import org.eclipse.xtext.testing.InjectWith
import org.eclipse.xtext.testing.XtextRunner
import org.junit.Test
import org.junit.runner.RunWith

import static org.agileware.natural.jbehave.tests.JbehaveTestHelpers.*
import static org.hamcrest.MatcherAssert.*
import static org.hamcrest.Matchers.*

@RunWith(XtextRunner)
@InjectWith(JbehaveInjectorProvider)
class JbehaveParsingTest {
	
	@Inject JbehaveTestHelpers _th
	
	@Test
	def void narrativeTypeA() {
		val model = _th.parse('''
			Narrative:
			In order to sell a pet
			As a store owner
			I want to add a new pet to the catalog
		''')
		
		assertThat(model, notNullValue())
		_th.trace("narrativeTypeA", model)

		val narrative = model.narrative as NarrativeA
		assertThat(narrative, notNullValue())
		assertThat(narrative.inOrderTo, notNullValue())
		assertThat(narrative.asA, notNullValue())
		assertThat(narrative.wantTo, notNullValue())
	}
	
	@Test
	def void narrativeTypeB() {
		val model = _th.parse('''
			Narrative:
			As a store owner
			I want to add a new pet to the catalog
			So that I can sell a pet
		''')
		
		assertThat(model, notNullValue())
		_th.trace("narrativeTypeB", model)
		
		val narrative = model.narrative as NarrativeB
		assertThat(narrative, notNullValue())
		assertThat(narrative.asA, notNullValue())
		assertThat(narrative.wantTo, notNullValue())
		assertThat(narrative.soThat, notNullValue())
	}
	
	@Test
	def void narrativeWithDescriptionAndMeta() {
		val model = _th.parse('''
			The quick brown fox
			Jumps over the lazy dog
			
			Meta:
			@author Mauro
			@themes UI Usability
			
			Narrative:
			In order to sell a pet
			As a store owner
			I want to add a new pet to the catalog
		''')
		
		assertThat(model, notNullValue())
		_th.trace("narrativeWithDescriptionAndMeta", model)
		
		assertThat(model.description, notNullValue())
		assertThat(model.description.lines, hasSize(2))
		assertThat(model.meta, notNullValue())
		assertThat(model.narrative, notNullValue())
	}
	
	@Test
	def void simpleScenarios() {
		val model = _th.parse('''
			Narrative:
			In order to communicate effectively to the business some functionality
			As a development team
			I want to use Behaviour-Driven Development
			
			Scenario: A scenario is a collection of executable steps of different type
			
			Given step represents a precondition to an event
			When step represents the occurrence of the event
			Then step represents the outcome of the event
			
			Scenario: Another scenario exploring different combination of events
			 
			Given a [precondition]
			When a negative event occurs
			Then a the outcome should [be-captured]    
			 
			Examples: 
			|precondition|be-captured|
			|abc|be captured    |
			|xyz|not be captured|
		''')
		
		assertThat(model, notNullValue())
		_th.trace("simpleScenarios", model)
		
		assertThat(model.narrative, notNullValue())
		
		val scenarios = model.scenarios
		assertThat(scenarios, hasSize(2))
	}
	
	@Test
	def void allSupportedSyntax() {
		val model = _th.parse(EXAMPLE_STORY)
		
		assertThat(model, notNullValue())
		_th.trace("allSupportedSyntax", model)
		
		assertThat(model.eResource.errors, empty())
	}
}
